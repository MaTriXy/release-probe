plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'project-report'
}

apply from: "$project.rootDir/gradle/versioning.gradle"

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    buildFeatures {
        buildConfig = true
        viewBinding = true
        resValues = true
    }

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        applicationId 'reactivecircus.releaseprobe'
        versionCode buildVersionCode()
        versionName buildVersionName()
        archivesBaseName = "ReleaseProbe-$versionName"

        testApplicationId 'reactivecircus.releaseprobe.test'
        testInstrumentationRunner "reactivecircus.releaseprobe.IntegrationTestRunner"

        // only support English for now
        resConfigs "en"
    }

    lintOptions {
        disable 'ParcelCreator'
        disable 'GoogleAppIndexingWarning'
        quiet false
        ignoreWarnings false
        htmlReport true
        xmlReport true
        htmlOutput file("$buildDir/reports/lint/lint-reports.html")
        xmlOutput file("$buildDir/reports/lint/lint-reports.xml")
        checkDependencies true
        ignoreTestSources true
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('secrets/debug.keystore')
            storePassword 'rp-debug'
            keyAlias 'rp-key'
            keyPassword 'rp-debug'
        }
        release {
            storeFile rootProject.file('secrets/release-probe.keystore')
            storePassword envOrProp('RELEASE_PROBE_STORE_PASSWORD')
            keyAlias 'releaseprobe'
            keyPassword envOrProp('RELEASE_PROBE_KEY_PASSWORD')
        }
    }

    bundle {
        // only support English for now
        language.enableSplit = false
    }

    buildTypes {
        debug {
            testCoverageEnabled enableCoverage
            signingConfig signingConfigs.debug

        }
        release {
            if (rootProject.file('secrets/release-probe.keystore').exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles('shrinker-rules.pro')
        }
    }

    flavorDimensions "environment"

    productFlavors {
        mock {
            applicationIdSuffix ".mock"
        }
        prod {}
    }

    onVariantProperties {
        // default app_name
        def appName = "ReleaseProbe"
        
        if (buildType == "debug") {
            // override app name for LeakCanary
            addResValue("leak_canary_display_activity_label", "string", "ReleaseProbe Leaks", null)
            
            // concatenate productFlavor to app name for non-production builds
            if (flavorName != "prod") {
                appName += "-${flavorName}"
            }

            // concatenate buildType to app name for non-release builds
            appName += "-${buildType}"
        }

        // set app_name
        addResValue("app_name", "string", appName, null)

        if (flavorName == "mock") {
            addBuildConfigField("ENABLE_BUGSNAG", false, null)
            addBuildConfigField("ENABLE_ANALYTICS", false, null)
            manifestPlaceholders.put("bugsnagApiKey", "")
        }
        else if (flavorName == "prod") {
            addBuildConfigField("ENABLE_BUGSNAG", true, null)
            addBuildConfigField("ENABLE_ANALYTICS", true, null)
            manifestPlaceholders.put("bugsnagApiKey", "${envOrProp("RELEASE_PROBE_BUGSNAG_API_KEY")}")
        }
    }

    // filter out mockRelease and prodDebug (when on CI) builds.
    android.variantFilter { variant ->
        variant.getFlavors().each { flavor ->
            if (flavor.name != "prod" && variant.buildType.name == "release" ||
                    isCiBuild && flavor.name == "prod" && variant.buildType.name == "debug") {
                variant.setIgnore(true)
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }
}

dependencies {
    implementation project(':bugsnag-tree')
    prodImplementation project(':analytics-api-firebase')
    mockImplementation project(':analytics-api-no-op')

    implementation project(':core-ui')
    implementation project(':ui-feeds')
    implementation project(':ui-watchlist')
    implementation project(':ui-browse-collections')
    implementation project(':ui-artifact-list')
    implementation project(':ui-settings')

    implementation project(':data')
    prodImplementation project(':remote-api-real')
    mockImplementation project(':remote-api-mock')

    implementation project(':background-work')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

    // AndroidX
    implementation "androidx.coordinatorlayout:coordinatorlayout:${versions.androidx.coordinatorLayout}"
    implementation "androidx.work:work-runtime:${versions.androidx.work}"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinx.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinx.coroutines}"

    // Bugsnag
    implementation "com.bugsnag:bugsnag-android:${versions.bugsnag}"

    // Koin
    implementation "org.koin:koin-core:${versions.koin}"
    implementation "org.koin:koin-androidx-viewmodel:${versions.koin}"

    // Firebase analytics
    prodImplementation "com.google.firebase:firebase-analytics-ktx:${versions.firebase.analytics}"

    // Enable LeakCanary for debug builds
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"
    // Fix SDK leaks
    implementation "com.squareup.leakcanary:plumber-android:${versions.leakCanary}"

    // Unit tests
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "io.mockk:mockk:${versions.mockk}"
    testImplementation "org.amshove.kluent:kluent-android:${versions.kluent}"
    testImplementation "androidx.arch.core:core-testing:${versions.androidx.arch}"
    testImplementation "org.koin:koin-test:${versions.koin}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlinx.coroutines}"
    testImplementation project(':coroutines-testing')

    // Android tests
    androidTestImplementation project(':testing-infra')
}

tasks.configureEach { task ->
    // disable google services plugin for mock flavor
    if (task.name ==~ /process(?i)mock.+GoogleServices/) {
        task.onlyIf { false }
    }
    // disable all AndroidTest tasks for prod flavor
    else if (task.name ==~ /.*(?i)prod.+AndroidTest.*/) {
        task.onlyIf { false }
    }
}
